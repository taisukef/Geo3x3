module geo3x3

function encode_fn(code, level, i, lat, lng, unit) =
  if (i >= level)
    code
  else
    encode_fn(
      code + "\(lng ~/ unit + lat ~/ unit * 3 + 1)",
      level,
      i + 1,
      lat - unit * (lat ~/ unit),
      lng - unit * (lng ~/ unit),
      unit / 3.0
    )

function encode(lat, lng, level) =
  encode_fn(
    if (lng < 0.0) "W" else "E",
    level,
    1,
    lat + 90.0,
    if (lng < 0.0) lng + 180.0 else lng,
    180.0 / 3.0
  )

function decode_fn(code, _lat, _lng, _level, _unit, wflg) =
  if (code.length == _level)
    new {
      lat = -90.0 + (_lat + _unit * 3.0 / 2.0)
      lng = if (wflg)
          (_lng + _unit * 3.0 / 2.0) - 180.0
        else
          _lng + _unit * 3.0 / 2.0
      level = _level
      unit = _unit * 3.0
    }
  else
    let (n = code[_level].toInt() - 1)
    decode_fn(
      code,
      _lat + n ~/ 3 * _unit,
      _lng + n % 3 * _unit,
      _level + 1,
      _unit / 3.0,
      wflg
    )

function decode(code) =
  if (code[0] == "W")
    decode_fn(code, 0.0, 0.0, 1, 180.0 / 3.0, true)
  else
    decode_fn(code, 0.0, 0.0, 1, 180.0 / 3.0, false)
